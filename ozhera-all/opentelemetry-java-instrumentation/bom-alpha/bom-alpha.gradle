plugins {
  id("java-platform")
}

apply plugin: "otel.publish-conventions"

description = "OpenTelemetry Instrumentation Bill of Materials (Alpha)"
group = "io.opentelemetry.instrumentation"
archivesBaseName = "opentelemetry-instrumentation-bom-alpha"

rootProject.subprojects.forEach { subproject ->
  if (!subproject.name.startsWith("bom")) {
    evaluationDependsOn(subproject.path)
  }
}

javaPlatform {
  allowDependencies()
}

def otelVersion = findProperty("otelVersion")

dependencies {
  api(platform("run.mone:opentelemetry-bom:${otelVersion}"))
  api(platform("run.mone:opentelemetry-bom-alpha:${otelVersion}"))
}

afterEvaluate {
  dependencies {

    constraints {
      rootProject.subprojects.sort { "$it.archivesBaseName" }
        .collect { it }
        .findAll { it.name != project.name && it.name != 'javaagent'}
        .forEach { project ->
          project.plugins.withId("maven-publish") {
            api(project)
          }
        }
    }
  }
}
