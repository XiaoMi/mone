ext.skipPublish = true

apply from: "$rootDir/gradle/instrumentation.gradle"

dependencies {
  implementation project(':testing-common:library-for-integration-tests')

  testCompileOnly project(':instrumentation-api')
  testCompileOnly project(':javaagent-api')
  testCompileOnly project(':javaagent-tooling')
  testCompileOnly project(':javaagent-extension-api')

  testImplementation "net.bytebuddy:byte-buddy"
  testImplementation "net.bytebuddy:byte-buddy-agent"

  testImplementation "com.google.guava:guava"
  testImplementation "run.mone:opentelemetry-extension-annotations"

  testImplementation "cglib:cglib:3.2.5"

  // test instrumenting java 1.1 bytecode
  // TODO do we want this?
  testImplementation "net.sf.jt400:jt400:6.1"
}

test {
  filter {
    excludeTestsMatching 'context.FieldInjectionDisabledTest'
  }
  // this is needed for AgentInstrumentationSpecificationTest
  jvmArgs '-Dotel.javaagent.exclude-classes=config.exclude.packagename.*,config.exclude.SomeClass,config.exclude.SomeClass$NestedClass'
}
test.finalizedBy(tasks.register("testFieldInjectionDisabled", Test) {
  filter {
    includeTestsMatching 'context.FieldInjectionDisabledTest'
  }
  jvmArgs '-Dotel.javaagent.experimental.field-injection.enabled=false'
})
