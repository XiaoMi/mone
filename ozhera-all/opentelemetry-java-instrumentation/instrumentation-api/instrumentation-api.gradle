plugins {
  id 'org.xbib.gradle.plugin.jflex' version '1.5.0'
}

group = 'io.opentelemetry.instrumentation'

apply plugin: "otel.java-conventions"
apply plugin: "otel.jacoco-conventions"
apply plugin: "otel.publish-conventions"

def jflexTargetDir = file"${project.buildDir}/generated/jflex/sql"

def sqlSanitizerJflex = tasks.register("sqlSanitizerJflex", org.xbib.gradle.plugin.JFlexTask) {
  group = 'jflex'
  description = 'Generate SqlSanitizer'
  source = [file("${project.projectDir}/src/main/jflex/SqlSanitizer.flex")]
  target = jflexTargetDir
}

tasks.named("compileJava").configure {
  dependsOn(sqlSanitizerJflex)
}

tasks.named("javadoc").configure {
  dependsOn(sqlSanitizerJflex)
}

tasks.named("sourcesJar").configure {
  dependsOn(sqlSanitizerJflex)
}

sourceSets.main.java.srcDir(jflexTargetDir)

dependencies {
  api project(":instrumentation-api-caching")

  api "run.mone:opentelemetry-api"
  api "run.mone:opentelemetry-semconv"

  implementation "run.mone:opentelemetry-api-metrics"
  implementation "org.slf4j:slf4j-api"

  compileOnly "com.google.auto.value:auto-value-annotations"
  annotationProcessor "com.google.auto.value:auto-value"

  testImplementation project(':testing-common')
  testImplementation "org.mockito:mockito-core"
  testImplementation "org.mockito:mockito-junit-jupiter"
  testImplementation "org.assertj:assertj-core"
  testImplementation "org.awaitility:awaitility"
  testImplementation "run.mone:opentelemetry-sdk-metrics"
  testImplementation "run.mone:opentelemetry-sdk-testing"
}
