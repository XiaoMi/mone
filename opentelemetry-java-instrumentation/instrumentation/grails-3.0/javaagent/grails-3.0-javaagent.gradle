apply from: "$rootDir/gradle/instrumentation.gradle"

muzzle {
  pass {
    group = "org.grails"
    module = "grails-web-url-mappings"
    versions = "[3.0,)"
    // version 3.1.15 depends on org.grails:grails-datastore-core:5.0.13.BUILD-SNAPSHOT
    // which (obviously) does not exist
    // version 3.3.6 depends on org.grails:grails-datastore-core:6.1.10.BUILD-SNAPSHOT
    // which (also obviously) does not exist
    skip('3.1.15', '3.3.6')
    assertInverse = true
  }
}

repositories {
  mavenCentral()
  maven {
    url "https://repo.grails.org/artifactory/core"
    mavenContent {
      releasesOnly()
    }
  }
  mavenLocal()
}

// first version where our tests work
def grailsVersion = '3.0.6'
def springBootVersion = '1.2.5.RELEASE'

dependencies {
  library("org.grails:grails-plugin-url-mappings:$grailsVersion")

  testInstrumentation project(':instrumentation:servlet:servlet-3.0:javaagent')
  testInstrumentation project(':instrumentation:servlet:servlet-javax-common:javaagent')
  testInstrumentation project(':instrumentation:tomcat:tomcat-7.0:javaagent')
  testInstrumentation project(':instrumentation:spring:spring-webmvc-3.1:javaagent')

  testLibrary "org.springframework.boot:spring-boot-autoconfigure:$springBootVersion"
  testLibrary "org.springframework.boot:spring-boot-starter-tomcat:$springBootVersion"

  testImplementation(project(':testing-common')) {
    exclude group: 'org.eclipse.jetty', module: 'jetty-server'
  }

  latestDepTestLibrary("org.grails:grails-plugin-url-mappings:4.0.+")
  latestDepTestLibrary "org.springframework.boot:spring-boot-autoconfigure:2.1.17.RELEASE"
  latestDepTestLibrary "org.springframework.boot:spring-boot-starter-tomcat:2.1.17.RELEASE"
}