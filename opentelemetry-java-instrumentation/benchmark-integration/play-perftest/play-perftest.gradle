plugins {
  id "org.gradle.playframework" version "0.9"
}

afterEvaluate {
  // assemble fails without this because gradle play plugin looks for the jar file without
  // considering the updated archivesBaseName that we have set in java.gradle
  archivesBaseName = 'play-perftest'
}

ext {
  playVersion = "2.6.20"
  scalaVersion = System.getProperty("scala.binary.version", /* default = */ "2.12")
}

play {
  platform {
    playVersion = project.ext.playVersion
    scalaVersion = project.ext.scalaVersion
    javaVersion = JavaVersion.VERSION_1_8
  }
  injectedRoutesGenerator = true
}

dependencies {
  implementation "com.typesafe.play:play-guice_$scalaVersion:$playVersion"
  implementation "com.typesafe.play:play-logback_$scalaVersion:$playVersion"
  implementation "com.typesafe.play:filters-helpers_$scalaVersion:$playVersion"

  implementation project(':javaagent-bootstrap')
  implementation project(':benchmark-integration')
}

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    name "lightbend-maven-releases"
    url "https://repo.lightbend.com/lightbend/maven-release"
  }
  ivy {
    name "lightbend-ivy-release"
    url "https://repo.lightbend.com/lightbend/ivy-releases"
    layout "ivy"
  }
}
